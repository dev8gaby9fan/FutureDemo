if (tradeIsApp.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
android {
    compileSdkVersion rootProject.ext.build_versions.compile_sdk

    defaultConfig {
        minSdkVersion rootProject.ext.build_versions.min_sdk
        targetSdkVersion rootProject.ext.build_versions.target_sdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
        multiDexEnabled true
        ndk {
            //## CTP TradeApi CPU架构
            //目前SDK支持armeabi/armeabi-v7a/arm64-v8a/x86/x86_64，不支持mips/mips64架构。
            abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a', "x86", "x86_64"
        }
        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }
    }
    //可以使用Parcelize注解，不需要事先Parcelable的抽象方法
    androidExtensions {
        experimental = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            if (tradeIsApp.toBoolean()) {
                manifest.srcFile 'src/main/manifest/app/AndroidManifest.xml'
            } else {//作为lib模块的时候，合并到宿主APP中去；排查app目录下的文件
                manifest.srcFile 'src/main/manifest/module/AndroidManifest.xml'
                resources {
                    exclude 'src/main/manifest/app/*'
                }
            }
        }
    }
    repositories {
        flatDir {
            dirs "libs"
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api project(":lib_common")
    implementation(name: "trade_api_release_v1.5", ext: "aar")
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    kapt deps.arouter.compiler
    kapt deps.androidx.room_compiler
    testImplementation deps.test.junit
    androidTestImplementation deps.test.ext
    androidTestImplementation deps.test.espresso_core
    androidTestImplementation deps.androidx.room_testing
}
